include: "common.smk"

pepfile: config["pepfile"]

samples = pep.sample_table["sample_name"]

rule all:
    input:
        kmt2a_csv=expand("{sample}/itd/{sample}.kmt2a.csv", sample=samples),
        kmt2a_bg_csv=expand("{sample}/itd/{sample}.kmt2a.bg.csv", sample=samples),
        kmt2a_plots=expand("{sample}/itd/{sample}.kmt2a.png", sample=samples),
        flt3_csv=expand("{sample}/itd/{sample}.flt3.csv", sample=samples),
        flt3_bg_csv=expand("{sample}/itd/{sample}.flt3.bg.csv", sample=samples),
        flt3_plots=expand("{sample}/itd/{sample}.flt3.png", sample=samples),

## KMT2A ##
rule align_kmt2a:
    input:
        fq1=get_forward,
        fq2=get_reverse,
        fasta=settings["kmt2a_fasta"]
    output:
        bam="{sample}/itd/{sample}.kmt2a.bam",
        bai="{sample}/itd/{sample}.kmt2a.bai",
    params:
        rg="@RG\\tID:{sample}\\tSM:{sample}"
    threads: 3
    singularity: containers["bwa-0.7.17-samtools-1.3.1-picard-2.9.2"]
    shell:
        "bwa mem -R \'{params.rg}\' -t {threads} -L 2,2 {input.fasta} {input.fq1} {input.fq2}"
        " | samtools view -Sbh -F 0x4 -"
        " | picard -Xmx4G SortSam I=/dev/stdin O={output.bam} SO=coordinate CREATE_INDEX=true"

rule detect_itd_kmt2a:
    input:
        bam=rules.align_kmt2a.output.bam,
        bai=rules.align_kmt2a.output.bai,
        ref=settings["kmt2a_fasta"],
    params:
        trx_name=settings["kmt2a_name"],
        start=settings["kmt2a_start"],
        end=settings["kmt2a_end"],
    output:
        csv="{sample}/itd/{sample}.kmt2a.csv",
        bg_csv="{sample}/itd/{sample}.kmt2a.bg.csv",
    singularity: containers["rose"]
    shell:
        "rose-dt -r {params.trx_name}:{params.start}-{params.end} --bg-counts {output.bg_csv} {input.ref} {input.bam} > {output.csv}"

rule plot_itd_kmt2a:
    input:
        csv=rules.detect_itd_kmt2a.output.csv,
        bg_csv=rules.detect_itd_kmt2a.output.bg_csv,
    output:
        png="{sample}/itd/{sample}.kmt2a.png",
    singularity: containers["rose"]
    shell:
        "rose-dt-plot.py"
        " --sample-id {wildcards.sample} --mode per-event"
        " {input.csv} {input.bg_csv} {output.png}"

## FLT3 ##
rule align_flt3:
    input:
        fq1=get_forward,
        fq2=get_reverse,
        fasta=settings["flt3_fasta"]
    output:
        bam="{sample}/itd/{sample}.flt3.bam",
        bai="{sample}/itd/{sample}.flt3.bai",
    params:
        rg="@RG\\tID:{sample}\\tSM:{sample}"
    threads: 3
    singularity: containers["bwa-0.7.17-samtools-1.3.1-picard-2.9.2"]
    shell:
        "bwa mem -R \'{params.rg}\' -t {threads} -L 2,2 {input.fasta} {input.fq1} {input.fq2}"
        " | samtools view -Sbh -F 0x4 -"
        " | picard -Xmx4G SortSam I=/dev/stdin O={output.bam} SO=coordinate CREATE_INDEX=true"

rule detect_itd_flt3:
    input:
        bam=rules.align_flt3.output.bam,
        bai=rules.align_flt3.output.bai,
        ref=settings["flt3_fasta"],
    params:
        trx_name=settings["flt3_name"],
        start=settings["flt3_start"],
        end=settings["flt3_end"],
    output:
        csv="{sample}/itd/{sample}.flt3.csv",
        bg_csv="{sample}/itd/{sample}.flt3.bg.csv",
    singularity: containers["rose"]
    shell:
        "rose-dt -r {params.trx_name}:{params.start}-{params.end} --bg-counts {output.bg_csv} {input.ref} {input.bam} > {output.csv}"

rule plot_itd_flt3:
    input:
        csv=rules.detect_itd_flt3.output.csv,
        bg_csv=rules.detect_itd_flt3.output.bg_csv,
    output:
        png="{sample}/itd/{sample}.flt3.png",
    singularity: containers["rose"]
    shell:
        "rose-dt-plot.py"
        " --sample-id {wildcards.sample} --mode per-event"
        " {input.csv} {input.bg_csv} {output.png}"
